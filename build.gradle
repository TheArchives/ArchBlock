buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
        classpath 'de.undercouch:gradle-download-task:1.2'
    }
}

plugins {
    id 'java'
    id 'findbugs'
    id 'com.github.johnrengelman.shadow' version '1.2.0'
    id "de.undercouch.download" version "1.2"
    id 'maven-publish'
    id 'ivy-publish'
}

version = "0.1.1"
sourceCompatibility = 1.7
group = "com.archivesmc.archblock"

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

publishing {
    publications {
        ivyJava(IvyPublication) {
            from components.java
        }
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        ivy {
            url "/var/www/gserv/cherry/repos/ivy/"
        }

        maven {
            url "/var/www/gserv/cherry/repos/maven/"
        }
    }
}

task runJarJar(type: Exec) {
    workingDir '.'

    commandLine 'java', '-jar', 'lib/jarjar-1.4.jar',
            'process', 'jarjar.rules',
            "build/libs/ArchBlock-${version}-all.jar",
            "build/ArchBlock-${version}.jar"
}

defaultTasks 'shadowJar', 'runJarJar'

download {
    src 'https://jarjar.googlecode.com/files/jarjar-1.4.jar'
    dest 'lib/jarjar-1.4.jar'
}
download {
    src 'http://dev.bukkit.org/media/files/716/564/WatchBlock.jar'
    dest 'lib/WatchBlock.jar'
}

repositories {
    mavenCentral()

    maven {
        url "http://repo.bukkit.org/content/groups/public/"
        name "Bukkit"
    }

    maven {
        url "http://nexus.theyeticave.net/content/repositories/pub_releases"
        name "Vault"
    }

    maven {
        url "https://hub.spigotmc.org/nexus/content/groups/public/"
        name "Spigot"
    }

    maven {
        url "http://maven.sk89q.com/artifactory/repo/"
        name "sk89q"
    }

    maven {
        url "http://maven.mewin.de/"
        name "mewin"
    }

    maven {
        url "http://repo.spongepowered.org/maven"
        name "sponge"
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile "org.bukkit:bukkit:1.7.9-R0.2"
    compile "net.milkbowl.vault:VaultAPI:1.5"
    compile "com.sk89q.worldedit:worldedit-bukkit:6.0.1"
    compile "com.sk89q:worldguard:6.0.0-SNAPSHOT"
    compile "com.mewin:WGCustomFlags:1.8.1-beta"
    compile "mysql:mysql-connector-java:5.1.34"
    compile "org.hibernate:hibernate-core:4.2.5.Final"
    compile "org.hibernate:hibernate-c3p0:4.2.5.Final"
    compile "commons-io:commons-io:2.4"
    compile "com.intellij:annotations:12.0"
    compile "org.spongepowered:spongeapi:2.1-SNAPSHOT"

    compile files('lib/WatchBlock.jar')
}

jar {
    from sourceSets.main.java
}

shadowJar {
    from sourceSets.main.java
}

javadoc {
    // Apparently this fixes things
    options.addStringOption("sourcepath", "")
}
